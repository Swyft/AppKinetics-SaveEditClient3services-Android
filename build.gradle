apply plugin: 'com.android.application'

buildscript {

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }

    repositories {
        mavenCentral()
        google()
        jcenter()

        def localProperties = new File(rootDir, "local.properties")
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        maven { url sdkDir+'/extras/blackberry/dynamics_sdk/m2repository' }
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()

        def localProperties = new File(rootDir, "local.properties")
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        maven { url sdkDir+'/extras/blackberry/dynamics_sdk/m2repository' }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.0'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        androidTest {
            manifest.srcFile 'tests/AndroidManifest.xml'
            java.srcDirs = ['tests']
            assets.srcDirs = ['tests/assets']
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.good.gd.example.appkinetics.saveeditclient"
        minSdkVersion 24
        //Ignoring Lint warning for OldTargetApi as this app intentionally sets target API level to 23
        //noinspection OldTargetApi
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        missingDimensionStrategy 'product', 'dynamics'
    }

    defaultPublishConfig "AppDebug"
    testBuildType "debug"

    flavorDimensions 'default'
    productFlavors {
        App { }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            testProguardFile 'proguard-test-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.blackberry.blackberrydynamics:android_handheld_platform:9.0.0.72'
    implementation 'com.blackberry.blackberrydynamics:android_handheld_backup_support:9.0.0.72'
    //implementation 'com.blackberry.blackberrydynamics:atsl:9.0.0.72'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    //Test Implementation Dependancies are used only when building app for android Automated Tests
    androidTestImplementation 'com.blackberry.blackberrydynamics:atsl:9.0.0.72'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.legacy:legacy-support-v4:1.0.0'
}

